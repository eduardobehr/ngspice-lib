** Balanced Three Phases Voltage Source
*
*
*                               rg      lg
*                                ↓      ↓
*                   |----(~)----⊏⊐----cccc---- Ua
*                   |
*   Uneutral -------●----(~)----⊏⊐----cccc---- Ub
*                   |
*                   |----(~)----⊏⊐----cccc---- Uc
*
*

.subckt VSOURCE3PH Ua Ub Uc Uneutral   amp=311 fg=60 rg=1m lg=10uH
.param pi2 = 6.283185307179586
* sources to resistors
    BVa      vra     Uneutral        v = amp * sin(pi2 * fg * time)
    BVb      vrb     Uneutral        v = amp * sin(pi2 * fg * time - 2.0943951023931953)
    BVc      vrc     Uneutral        v = amp * sin(pi2 * fg * time + 2.0943951023931953)

* resistors to inductances
    Ra      vra     vla     {rg}
    Rb      vrb     vlb     {rg}
    Rc      vrc     vlc     {rg}

* inductances to output
    La      vla     Ua      {lg}
    Lb      vlb     Ub      {lg}
    Lc      vlc     Uc      {lg}

.ends VSOURCE3PH
