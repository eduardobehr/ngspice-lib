*****************************************************************
* Level2 Version of 1200V DIODE Models	January 2002
*****************************************************************
* INFINEON TECHNOLOGIES AG      	IDX_E120_L2.LIB		*
*								*
*  (V1.5)     	01/02  						*
*                                                               *
*****************************************************************
* simulation support:  simulate@infineon.com                    *
*                                                               *
* internet          :  www.infineon.com\simulate                * 
*****************************************************************                                                               *
*                                                               *
* Models provided by INFINEON are not warranted by INFINEON as  *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    * 
* typical device.                                               * 
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. INFINEON therefore does not  *
* assume any liability arising from their use.                  *
* INFINEON reserves the right to change models without prior    * 
* notice.                                                       *
*                                                               *
* This library contains Level 2 models for the following	* 
* INFINEON Technologies 1200 V EMCON HE Power Diodes		*
* devices/chip-models including lead inductances related	*
*								*
* 								*
* IDP04E120 = IDB04E120						*
* IDP09E120 = IDB09E120						*
* IDP12E120 = IDB12E120						*
* IDP18E120 = IDB18E120						*
* IDP30E120 = IDB30E120						*
* 								*
* the model is based on the publication:			*
* 								*
* R.Kraus, P.Türkes, J.Sigg					*
* Physics-based Models Of Power Semiconductor Devices 		*
* For The Circuit Simulator Spice				*
* 								*
* Power Electronics Specialists Conference, 1998.		* 
* PESC 98 Record. 29th Annual IEEE				*
*								*
* SUPPORTemail:	simulate@infineon.com				*
****************************************************************
.SUBCKT IDP04E120_L2 ano kat PARAMS: TJ = 27
LANO	ano	anol		5n
LKAT	katl	kat		7n	
XDIO	anol     katl		L4XXX-ET	PARAMS: TJ = {TJ} A=0.0132	N_ideal = 1.05
.ENDS
.SUBCKT IDP09E120_L2 ano kat PARAMS: TJ = 27 
LANO	ano	anol		5n
LKAT	katl	kat		7n	
XDIO	anol     katl		L4XXX-ET	PARAMS: TJ = {TJ} A=0.0324	N_ideal = 1.05
.ENDS
.SUBCKT IDP12E120_L2 ano kat PARAMS: TJ = 27 
LANO	ano	anol		5n
LKAT	katl	kat		7n	
XDIO	anol     katl		L4XXX-ET	PARAMS: TJ = {TJ} A=0.0473	N_ideal = 1.05
.ENDS
.SUBCKT IDP18E120_L2 ano kat PARAMS: TJ = 27 
LANO	ano	anol		5n
LKAT	katl	kat		7n	
XDIO	anol     katl		L4XXX-ET	PARAMS: TJ = {TJ} A=0.065	N_ideal = 1.05
.ENDS
.SUBCKT IDP30E120_L2 ano kat PARAMS: TJ = 27 
LANO	ano	anol		5n
LKAT	katl	kat		7n	
XDIO	anol     katl		L4XXX-ET	PARAMS: TJ = {TJ} A=0.098	N_ideal = 1.05
.ENDS
.SUBCKT	L4XXX-ET anode	kath PARAMS: TJ= 27 A = 1 N_ideal = 1
.PARAM
+q = 1.602e-19		eps0 = 8.85e-14	epsi = 11.8
+kb = 1.38e-23		T0 = 273	ni0 = 1.45e10
+un = 1350		up = 450 	tau = 5u	w0 = 115e-4
+vlimit =70e6 BV = 1400
.PARAM  Nd = 1e14
.PARAM  Vdiff=  595u
.PARAM  Ut =	25.8m
.PARAM  D =	17.44
.PARAM  L =	9.34m
.PARAM	Ise0 =	{A*652p}
.PARAM	Ise_g  = {A*0.56p}
.PARAM  Ism0 =	{A*8.67u}
.PARAM	Ism_g = {A*3.204e-19}
.PARAM  CJ0 =	{A*3.46n}
.PARAM  QN =	{A*0.184u}
.PARAM  XF =	3610
.PARAM	VPT=	1000
.PARAM  a0 =	-0.544
.PARAM  a1 =	{0.3*(1 + a0)/(1 - a0)}
.PARAM  a2 =	{0.1/(1 - 0.5*(1 - a1*a1)*(1 - a0))}
.PARAM  Rd0 =	{0.53/A} 
.PARAM	Ra =	{1.23m/A}
RSERIE	anode	ano	{Ra}
GDE	ano	mi	VALUE = {((ISE(TJ)**((t0 + TJ)/(t0 + 27)))/ISE(27))**((t0 + 27)/(t0 + TJ)) 
+ * MAX(I(VDE),0)**((t0 + 27)/(t0 + TJ)) - MAX(I(VDE),0)}
GD0	ano	mi1	VALUE = {((ISM(TJ)**((t0 + TJ)/(t0 + 27)))/ISM(27))**((t0 + 27)/(t0 + TJ))
+ * MAX(I(VD0),0)**((t0 + 27)/(t0 + TJ)) - MAX(I(VD0),0)}
VDE	ano	ano1	0
DE	ano1	mi	D1
D0	ano2	mi1	D2
VD0	ano	ano2	0
.model	D1	D	(is={Ise0}, N= {N_ideal})
.model  D2	D	(is={Ism0}, N=2)
VID0	mi1	mi	0
VITOT	kat	kathv	0
EVDIFF	kathv	kath	VALUE = {(TJ + t0)* Vdiff}
EGIR	mi	kat	VALUE = {I(VITOT)*Rd(V(q,0),TJ)}
GID	ano	mi	VALUE = {IQ(V(xj,0),I(VID0),V(q,0),I(VDXJ),TJ) - I(VIJCT)}
CQB	q	0	1u
RHELP	q	0	1meg
GRQB	q	0	VALUE = {1u*V(q)/(Tau*((TJ + t0)/300)**2) }
GIQ	0	q	VALUE = {MAX(I(VID0),0) + IQ(V(xj,0),I(VID0),V(q,0),I(VDXJ),TJ)}
EXJ	xj1 0	VALUE	{xj(V(ano,mi),MIN(I(VITOT),1e8))}
CXJ	xj  0	1n
VDXJ	xj1 xj	0
EJUNCT	jct 0	VALUE = {LIMIT(V(kat,ano),0,VPT)}
VIJCT	jct cp	0
DJCT	0   cp	DXXX
.MODEL  DXXX	D (is = 1p, CJO= {CJ0})
.FUNC   EG(T)			{1.16 - 7.02e-4*(T + t0)**2/(T + t0 + 1108)}
.FUNC	DA(T)			{2*kb*un*up/q/(un + up)*(T + t0)*(((T + t0)/300)**-1.5)}
.FUNC   ISE(T)  		{(T + t0)*un*(((T + t0)/300)**-1.5)*((ise_g*(ni0*((T + t0)/300)**1.5*exp(1/(2*8.61e-5)
+ *(-300*EG(T)+(t0 + T)*EG(27))/(T + t0)/300)))
+ *(kb*(ni0*((T + t0)/300)**1.5*exp(1/(2*8.61e-5)*(-300*EG(T)+(t0 + T)*EG(27))/(T + t0)/300))))}
.FUNC	ISM(T)			{Ism_g*(ni0*((T + t0)/300)**1.5*exp(1/(2*8.61e-5)*(-300*EG(T)+(t0 + T)*EG(27))/(T + t0)/300))
+ *sqrt(DA(T)/(Tau*((T + t0)/300)**2))}
.FUNC	xj(V,Y2)		{(XF*SQRT(1-limit(V,-VPT,0)))/SQRT(Nd + MAX(up*(-Y2)/(un + up),0)/A/q/vlimit)}
.FUNC	w(V)			{MIN(MAX(w0-V*(1 + a1),1e-4),w0-1e-4)}
.FUNC	Rd(Z,T)			{w0*w0/(un*QN*(((T + t0)/300)**-1.5) + (un + up)*MAX(Z,0)*1e-6*(((T + t0)/300)**-1.5))}
.FUNC	Td(V,U,T) 		{a2*w(V)*w(V)/DA(T)/(1 + w(V)*LIMIT(1e9*U,0,1e8)/4/DA(T))}
.FUNC	IQ(V,Y,Z,U,T)	{((Tau*((T + t0)/300)**2)*Y - MAX(Z,0)*1e-6)/(Td(V,U,T))}
.ends
***********************************************************
